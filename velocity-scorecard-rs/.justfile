set dotenv-load := true

_help:
	just -l

# Run the nightly formatter
fmt:
	cargo +nightly fmt

# Run tests.
test:
	cargo nextest run

# Run the same checks we run in CI. Requires nightly.
ci: test fmt
	cargo clippy --all-targets
	cargo test --doc

# Auto-fix clippy complaints.
lint:
	cargo clippy --fix --all-targets

# Tag a new version for release.
version BUMP:
	#!/usr/bin/env bash
	set -e
	current=$(tomato get package.version Cargo.toml)
	version=$(semver-bump {{BUMP}} "$current")
	tomato set package.version "$version" Cargo.toml &> /dev/null
	cargo generate-lockfile
	git commit Cargo.toml Cargo.lock -m "v${version}"
	git tag "v${version}"
	echo "Release tagged for version v${version}"
